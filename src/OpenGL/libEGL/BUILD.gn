# Copyright 2016 The SwiftShader Authors. All Rights Reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#    http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# Need a separate config to ensure the warnings are added to the end.
config("swiftshader_libEGL_private_config") {
  if (is_clang) {
    cflags = [
      "-DLOG_TAG=\"swiftshader_libEGL\"",
      "-DEGLAPI=",
      "-DEGL_EGLEXT_PROTOTYPES",
      "-Wno-sign-compare",
    ]

    if (!is_debug) {
      cflags += [
        "-DNDEBUG",
        "-DANGLE_DISABLE_TRACE",
      ]
    }
  }
}

shared_library("swiftshader_libEGL") {
  deps = [
    "//third_party/swiftshader/src/Common:swiftshader_common",
    "//third_party/swiftshader/src/Reactor:swiftshader_reactor",
    "//third_party/swiftshader/src/Renderer:swiftshader_renderer",
    "//third_party/swiftshader/src/OpenGL/common:swiftshader_opengl_common",
    "//third_party/swiftshader/src/OpenGL/compiler:swiftshader_opengl_compiler",
    "//third_party/swiftshader/src/Shader:swiftshader_shader",
    "//third_party/swiftshader/src/Main:swiftshader_main",
    "//third_party/swiftshader/third_party/LLVM:swiftshader_llvm",
  ]

  sources = [
    "Config.cpp",
    "Display.cpp",
    "Surface.cpp",
    "libEGL.cpp",
    "main.cpp",
  ]

  if (host_os == "mac") {
    sources += [
      "OSXUtils.mm",
    ]
  }

  configs += [
    ":swiftshader_libEGL_private_config",
  ]

  include_dirs = [
    "../../../include",
    "../..",
    "..",
  ]
}