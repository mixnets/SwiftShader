# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# Need a separate config to ensure the warnings are added to the end.
config("swiftshader_main_private_config") {
  if (is_clang) {
    cflags = [
      "-DLOG_TAG=\"swiftshader_main\"",
      "-msse2",
    ]

    if (!is_debug) {
      cflags += [
        "-DNDEBUG",
        "-DANGLE_DISABLE_TRACE",
      ]
    }
  }
}

source_set("swiftshader_main") {
  deps = [
    "//third_party/swiftshader/src/Common:swiftshader_common",
  ]

  sources = [
    "Config.cpp",
    "FrameBuffer.cpp",
    "SwiftConfig.cpp",
  ]

  if (host_os == "linux") {
    sources += [
      "libX11.cpp",
      "FrameBufferX11.cpp",
    ]
  } else if (host_os == "mac") {
    sources += [
      "FrameBufferOSX.mm",
    ]
  } else if (host_os == "win") {
    sources += [
      "FrameBufferDD.cpp",
      "FrameBufferGDI.cpp",
      "FrameBufferWin.cpp",
    ]
  }

  configs += [
    ":swiftshader_main_private_config",
  ]

  include_dirs = [
    "..",
    "../Common",
  ]
}